import com.github.gradle.node.npm.task.NpmTask

plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.2'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'com.github.node-gradle.node' version '7.0.1'
}

group = 'com.Moleugo'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	runtimeOnly 'com.mysql:mysql-connector-j'
}

node {
	version = '20.11.1'
	npmVersion = '10.2.4'
	download = true
	nodeProjectDir = file('frontend')
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.register('npmBuild', NpmTask) {
	dependsOn tasks.named('npmInstall')
	args = ['run', 'build']
}

tasks.register('copyFrontend', Copy) {
	dependsOn npmBuild
	from "${projectDir}/frontend/dist"
	into "${projectDir}/src/main/resources/static"
}

processResources.dependsOn(copyFrontend)